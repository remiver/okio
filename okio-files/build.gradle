apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  jvm {
    withJava()
  }
  if (kmpJsEnabled) {
    js {
      configure([compilations.main, compilations.test]) {
        tasks.getByName(compileKotlinTaskName).kotlinOptions {
          moduleKind = "umd"
          sourceMap = true
          metaInfo = true
        }
      }
      nodejs {
        testTask {
          useMocha {
            timeout = "30s"
          }
        }
      }
    }
  }
  if (kmpNativeEnabled) {
    macosX64()
    linuxX64()
    mingwX64()
  }
  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdLib.common
        api project(":okio")
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.annotations
        implementation deps.kotlin.time
        implementation project(":okio-testing")
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdLib.jdk6
      }
    }
    jvmTest {
      dependencies {
        implementation deps.test.junit
        implementation deps.test.assertj
        implementation deps.kotlin.test.jdk
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdLib.js
        implementation("org.jetbrains.kotlinx:kotlinx-nodejs:0.0.7")
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    posixMain {
      dependsOn commonMain
    }
    mingwX64Main {
      dependsOn posixMain
    }
    unixMain {
      dependsOn posixMain
    }
    appleMain {
      dependsOn unixMain
    }
    linuxMain {
      dependsOn unixMain
    }
    macosX64Main {
      dependsOn appleMain
      dependsOn unixMain
    }
    linuxX64Main {
      dependsOn linuxMain
      dependsOn unixMain
    }
  }
}
